/-
Copyright (c) 2025 [Your Name]. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: [Your Name], [Collaborator Name if applicable]
-/
module
public import Mathlib.Analysis.Complex.Harmonic.Analytic
public import Mathlib.MeasureTheory.Integral.CircleAverage

/-!
# The Poisson Integral Formula on Disc

## Main results

Theorems `poisson_integral_of_harmonicOn_disc_continuousOn_closedDisc` and
`poisson_integral_of_harmonicOn_disc_continuousOn_closedDisc_ker_re`:
Every function `u : ‚ÑÇ ‚Üí ‚Ñù` harmonic on the disc with radius `R` and center `0`, and
continuous on the closed disc, can be represented as
```
u(z) = 1/(2œÄ) ‚à´_0^{2œÄ} (R¬≤ - |z|¬≤) / |R * exp (it) - z|¬≤ * u(R * exp (it)) dt
     = 1/(2œÄ) ‚à´_0^{2œÄ} Re((R * exp (it) + z) / (R * exp (it) - z)) * u(R * exp (it)) dt,
```
for `z` in the disc.

Theorem `poisson_integral_of_diffContOnCl_disc` and `poisson_integral_of_diffContOnCl_disc_ker_re`:
Every function `f : ‚ÑÇ ‚Üí E` ‚ÑÇ-differentiable on the disc with radius `R` and center `0`, and
continuous on the closed disc, with values in a complex Banach space `E`, can be represented as
```
f(z) = 1/(2œÄ) ‚à´_0^{2œÄ} (R¬≤ - |z|¬≤) / |R * exp (it) - z|¬≤ ‚Ä¢ f(R * exp (it)) dt
     = 1/(2œÄ) ‚à´_0^{2œÄ} Re((R * exp (it) + z) / (R * exp (it) - z)) ‚Ä¢ f(R * exp (it)) dt,
```
for `z` in the disc.

## Implementation Notes

The proof follows from
- Cauchy Integral Formula,
- Cauchy-Goursat Theorem,
- a harmonic function is the real part of a holomorphic function on a disc,
- Lebesgue's Dominated Convergence Theorem.

## References

[Rudin, *Real and Complex Analysis* (Theorem 11.9)][rudin2006real]

## Tags

‚ÑÇ-differentiable function, harmonic function, Poisson integral.
-/

public section

open Complex Metric Real Set

/-- Scaling by `r ‚àà (0,1)` a point in a closed disc centerd at `0` is in the open disc. -/
lemma mem_disc_of_scaled {z : ‚ÑÇ} {r R : ‚Ñù} (hR : 0 < R)
    (hz : ‚Äñz‚Äñ ‚â§ R) (hr : r ‚àà Ioo 0 1) : r * z ‚àà ball 0 R := by
  rw [mem_ball, dist_zero_right, norm_mul, norm_real, norm_eq_abs, abs_of_pos hr.1]
  exact lt_of_le_of_lt (mul_le_mul_of_nonneg_left hz (le_of_lt hr.1))
                       ((mul_lt_iff_lt_one_left hR).mpr hr.2)

/-- `r * R * exp (t * I)` is in the disc of radius `R` and center `0`, for `r ‚àà (0,1)`. -/
lemma mem_disc_of_scaled_exp_ofReal_mul_I {r R : ‚Ñù} (hR : 0 < R) (hr : r ‚àà Ioo 0 1) (t : ‚Ñù) :
    r * R * exp (t * I) ‚àà ball 0 R := by
      rw [mul_assoc]
      apply mem_disc_of_scaled hR _ hr
      simp [norm_exp_ofReal_mul_I, abs_of_pos hR]


/-- `R * exp (t * I)` is not equal to any `z` in the disc of radius `R`, centered at `0`. -/
lemma neq_in_disc_of_mul_exp_ofReal_mul_I {z : ‚ÑÇ} {R : ‚Ñù}
    (hR : 0 < R) (hz : z ‚àà ball 0 R) (t : ‚Ñù) : R * exp (t * I) - z ‚â† 0 := by
  intro h
  rw [sub_eq_zero] at h
  simp [‚Üê h, mem_ball, dist_zero_right, norm_exp_ofReal_mul_I,
        norm_real, norm_eq_abs, abs_of_pos hR] at hz

/-- `R * star (exp (t * I))` is not equal to any `z` in the disc of radius `R`, centered at `0`. -/
lemma neq_in_disc_of_mul_star_exp_ofReal_mul_I {z : ‚ÑÇ} {R : ‚Ñù}
    (hR : 0 < R) (hz : z ‚àà ball 0 R) (t : ‚Ñù) : R * star (exp (t * I)) - star z ‚â† 0 := by
  have : R * star (exp (t * I)) = star (R * exp (t * I)) := by simp [star_mul']
  rw [this, ‚Üê star_sub]
  exact star_ne_zero.mpr (neq_in_disc_of_mul_exp_ofReal_mul_I hR hz t)

/-- The conjugate of `exp (t * I)` is its inverse. -/
lemma star_exp_ofReal_mul_I_eq_inv {t : ‚Ñù} : star (exp (t * I)) = (exp (t * I))‚Åª¬π := by
  rw [star_def, ‚Üê exp_conj, ‚Üê exp_neg (t * I)]
  simp

/-- `R ^ 2 - star z * w ‚â† 0`, for `z` in the disc with radius `R` and center `0`,
and for `w` in the closed disc. -/
lemma radius_sq_sub_star_mul_neq_zero {z : ‚ÑÇ} {w : ‚ÑÇ} {R : ‚Ñù} (hR : 0 < R)
    (hz : z ‚àà ball 0 R) (hw : w ‚àà closedBall 0 R) :
    R ^ 2 - star z * w ‚â† 0 := by
  intro h
  have hz_norm : ‚Äñz‚Äñ < R := by rw [mem_ball_zero_iff] at hz; exact hz
  have hw_norm : ‚Äñw‚Äñ ‚â§ R := mem_closedBall_zero_iff.mp hw
  have : ‚Äñstar z * w‚Äñ < R ^ 2 := by
    calc ‚Äñstar z * w‚Äñ ‚â§ ‚Äñstar z‚Äñ * ‚Äñw‚Äñ := norm_mul_le _ _
      _ = ‚Äñz‚Äñ * ‚Äñw‚Äñ := by rw [norm_star]
      _ < R ^ 2 := by  nlinarith [norm_nonneg z, norm_nonneg w]
  rw [sub_eq_zero] at h
  rw [‚Üê h] at this
  simp [norm_real, norm_eq_abs, abs_of_pos hR] at this

/-- If `f` is `‚ÑÇ`-differentiable on a disc centered at zero,
then `Œ∂ ‚Ü¶ f (r * Œ∂)` is differentiable at `z` for `r` in `(0,1)` and `z` in the closed disc. -/
lemma differentiableAt_of_differentiableOn_disc_of_mul {E : Type*} [NormedAddCommGroup E]
    [NormedSpace ‚ÑÇ E] {f : ‚ÑÇ ‚Üí E} {z : ‚ÑÇ} {r R : ‚Ñù} (hR : 0 < R)
    (hf : DifferentiableOn ‚ÑÇ f (ball 0 R)) (hz : z ‚àà closedBall 0 R) (hr : r ‚àà Ioo 0 1) :
    DifferentiableAt ‚ÑÇ (fun Œ∂ => f (r * Œ∂)) z := by
  rw [mem_closedBall, dist_zero_right] at hz
  exact DifferentiableAt.comp z (hf.differentiableAt
        (isOpen_ball.mem_nhds (mem_disc_of_scaled hR hz hr)))
        (differentiableAt_id.const_mul _)

/-- Cauchy's integral formula applied to `f` `‚ÑÇ`-differentiable on a disc centered at `0`,
at the point `r*z`, for `r` in `(0,1)` and `z` in the disc. -/
lemma cauchy_circleIntegral_formula_scaled_disc {E : Type*} [NormedAddCommGroup E]
    [NormedSpace ‚ÑÇ E] [CompleteSpace E] {f : ‚ÑÇ ‚Üí E} {z : ‚ÑÇ} {r R : ‚Ñù} (hR : 0 < R)
    (hf : DifferentiableOn ‚ÑÇ f (ball 0 R)) (hr : r ‚àà Ioo 0 1) (hz : z ‚àà ball 0 R) :
    f (r * z) = (1 / (2 * œÄ * I)) ‚Ä¢ ‚àÆ (Œ∂ : ‚ÑÇ) in C(0, R), (1 / (Œ∂ - z)) ‚Ä¢ f (r * Œ∂) := by
  have hfr_cont : ContinuousOn (fun Œ∂ => f (r * Œ∂)) (closedBall 0 R) :=
    fun x hx => (DifferentiableAt.continuousAt
            (differentiableAt_of_differentiableOn_disc_of_mul hR hf hx hr)).continuousWithinAt
  have := @circleIntegral_sub_inv_smul_of_differentiable_on_off_countable
    _ _ _ _ R 0 z (fun Œ∂ => f (r * Œ∂)) ‚àÖ countable_empty hz hfr_cont
  simp only [div_eq_inv_mul, mul_one]
  rw [this]
  ¬∑ simp only [smul_smul, inv_mul_cancel‚ÇÄ two_pi_I_ne_zero]
    exact Eq.symm (MulAction.one_smul (f (r * z)))
  ¬∑ intro x hx
    simp only [diff_empty] at hx
    exact differentiableAt_of_differentiableOn_disc_of_mul hR hf (ball_subset_closedBall hx) hr

/-- Cauchy's integral formula for `‚ÑÇ`-differentiable functions on a disc centred at 0,
evaluated at scaled points `r * z` with `r ‚àà (0,1)`. -/
lemma cauchy_integral_formula_scaled_disc {E : Type*} [NormedAddCommGroup E]
    [NormedSpace ‚ÑÇ E] [CompleteSpace E] {f : ‚ÑÇ ‚Üí E} {z : ‚ÑÇ} {r R : ‚Ñù} (hR : 0 < R)
    (hf : DifferentiableOn ‚ÑÇ f (ball 0 R)) (hr : r ‚àà Ioo 0 1) (hz : z ‚àà ball 0 R) :
    f (r * z) = (1 / (2 * œÄ)) ‚Ä¢ ‚à´ t in 0..2 * œÄ,
                (R * exp (t * I) / (R * exp (t * I) - z)) ‚Ä¢ f (r * R * exp (t * I)) := by
  have h_cauchy := cauchy_circleIntegral_formula_scaled_disc hR hf hr hz
  rw [‚Üê circleIntegral.integral_smul] at h_cauchy
  rw [‚Üê intervalIntegral.integral_smul, h_cauchy]
  simp only [circleIntegral]
  congr 1
  ext t
  have : f (r * circleMap 0 R t) = f (r * R * exp (t * I)) := by
    congr 1
    simp [circleMap, mul_assoc]
  rw [this]
  simp only [‚Üê smul_assoc]
  have : (deriv (circleMap 0 R) t ‚Ä¢ (1 / (2 * œÄ * I))) ‚Ä¢ (1 / (circleMap 0 R t - z)) =
         ((1 / (2 * œÄ)) ‚Ä¢ (R * exp (t * I) / (R * exp (t * I) - z))) := by
    simp only [deriv_circleMap, circleMap, zero_add, mul_inv_rev,
              div_eq_inv_mul, smul_eq_mul, real_smul, ofReal_mul,
              ofReal_inv, ofReal_ofNat, mul_one, mul_assoc]
    rw [‚Üê mul_assoc I I‚Åª¬π, mul_inv_cancel‚ÇÄ I_ne_zero, one_mul]
    ring_nf
  rw [this]

/-- If `f` is `‚ÑÇ`-differentiable on a disc centered at zero, then
`Œ∂ ‚Ü¶ (star z / (I * (R ^ 2 - star z * Œ∂))) ‚Ä¢ f (r * Œ∂)` is differentiable at `w`
in the closed disc with radius `R` and center `0`, for `r` in `(0,1)`. -/
lemma differentiableAt_goursat_integrand_scaled_disc
    {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚ÑÇ E] {f : ‚ÑÇ ‚Üí E} {z w : ‚ÑÇ} {r R : ‚Ñù}
    (hR : 0 < R) (hf : DifferentiableOn ‚ÑÇ f (ball 0 R)) (hr : r ‚àà Ioo 0 1)
    (hz : z ‚àà ball 0 R) (hw : w ‚àà closedBall 0 R) :
    DifferentiableAt ‚ÑÇ (fun Œ∂ => (star z / (I * (R ^ 2 - star z * Œ∂))) ‚Ä¢ f (r * Œ∂)) w := by
  refine DifferentiableAt.smul ?_ ?_
  ¬∑ refine DifferentiableAt.div (differentiableAt_const _) ?_ ?_
    ¬∑ apply DifferentiableAt.const_mul
      refine DifferentiableAt.sub (differentiableAt_const (R ^ 2 : ‚ÑÇ)) ?_
      exact DifferentiableAt.mul (differentiableAt_const (star z)) differentiableAt_id
    ¬∑ exact mul_ne_zero I_ne_zero (radius_sq_sub_star_mul_neq_zero hR hz hw)
  ¬∑ exact differentiableAt_of_differentiableOn_disc_of_mul hR hf hw hr

/-- We apply the Cauchy-Goursat theorem to the function
`Œ∂ ‚Ü¶ (star z / (I * (R ^ 2 - star z * Œ∂))) ‚Ä¢ (f (r * Œ∂)))`
on the circle of radius `R`, centered at `0`. -/
lemma vanishing_goursat_circleIntegral_scaled_disc
    {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚ÑÇ E] {f : ‚ÑÇ ‚Üí E} {z : ‚ÑÇ} {r R : ‚Ñù}
    (hR : 0 < R) (hf : DifferentiableOn ‚ÑÇ f (ball 0 R)) (hr : r ‚àà Ioo 0 1) (hz : z ‚àà ball 0 R) :
    (‚àÆ w in C(0, R), (star z / (I * (R ^ 2 - star z * w))) ‚Ä¢ f (r * w)) = 0 := by
  apply circleIntegral_eq_zero_of_differentiable_on_off_countable hR.le countable_empty
  ¬∑ exact fun Œ∂ hŒ∂ => (DifferentiableAt.continuousAt
          (differentiableAt_goursat_integrand_scaled_disc hR hf hr hz hŒ∂)).continuousWithinAt
  ¬∑ rw [diff_empty]
    exact fun Œ∂ hŒ∂ => differentiableAt_goursat_integrand_scaled_disc hR hf hr hz
                      (ball_subset_closedBall hŒ∂)

/-- An auxiliary identity that will be used in the integrand of the Cauchy-Goursat theorem. -/
lemma goursat_integrand_eq_aux (z : ‚ÑÇ) (t R : ‚Ñù) (hR : 0 < R) :
    star z / (R * star (exp (t * I)) - star z) =
    I * R * exp (t * I) * (star z / (I * (R ^ 2 - star z * R * exp (t * I)))) := by
  have : I * (R ^ 2 - star z * R * exp (t * I)) = I * R * (R - star z * exp (t * I)) := by
    ring_nf
  rw [this]
  have : I * R ‚â† 0 := by simp [I_ne_zero, ne_of_gt hR]
  rw [star_exp_ofReal_mul_I_eq_inv, mul_comm (I * R), mul_assoc, ‚Üê mul_div_assoc,
      mul_div_mul_left (hc := this), ‚Üê mul_div_assoc, mul_comm (exp (t * I)),
      mul_div_assoc, div_eq_mul_inv (star z)]
  congr 1
  rw [inv_eq_one_div]
  nth_rewrite 2 [‚Üê inv_inv (exp (t * I)), inv_eq_one_div]
  rw [div_div, mul_sub, mul_comm (star z), ‚Üê mul_assoc,
      inv_mul_cancel‚ÇÄ (Complex.exp_ne_zero (t * I)), one_mul]
  congr 1
  ring_nf

/-- The Cauchy-Goursat theorem for a disc centered at `0` implies the integral of a
`‚ÑÇ`-differentiable function against a conjugate Cauchy kernel vanishes. -/
lemma vanishing_goursat_integral_scaled_disc {E : Type*} [NormedAddCommGroup E]
    [NormedSpace ‚ÑÇ E] {f : ‚ÑÇ ‚Üí E} {z : ‚ÑÇ} {r R : ‚Ñù} (hR : 0 < R)
    (hf : DifferentiableOn ‚ÑÇ f (ball 0 R)) (hr : r ‚àà Ioo 0 1) (hz : z ‚àà ball 0 R) :
    ‚à´ t in 0..2 * œÄ, (star z / (R * star (exp (t * I)) - star z)) ‚Ä¢
      f (r * R * exp (t * I)) = 0 := by
  convert (vanishing_goursat_circleIntegral_scaled_disc hR hf hr hz) using 3
  rw [circleIntegral_def_Icc]
  rw [intervalIntegral.integral_of_le (mul_nonneg zero_le_two pi_pos.le)]
  congr 1
  ¬∑ exact MeasureTheory.Measure.restrict_congr_set MeasureTheory.Ioc_ae_eq_Icc
  ¬∑ funext Œ∏
    simp only [circleMap_zero, deriv_circleMap, smul_smul]
    nth_rewrite 1 [mul_comm (R * exp (Œ∏ * I)), ‚Üê mul_assoc I]
    rw [goursat_integrand_eq_aux z Œ∏ R hR]
    congr 1 <;> simp [mul_assoc]

/-- We put together `cauchy_integral_formula_scaled_disc` and
`vanishing_goursat_integral_scaled_disc`. -/
lemma cauchy_goursat_integral_scaled_disc {E : Type*} [NormedAddCommGroup E]
    [NormedSpace ‚ÑÇ E] [CompleteSpace E] {f : ‚ÑÇ ‚Üí E} {z : ‚ÑÇ} {r R : ‚Ñù} (hR : 0 < R)
    (hf : DifferentiableOn ‚ÑÇ f (ball 0 R)) (hr : r ‚àà Ioo 0 1) (hz : z ‚àà ball 0 R) :
    f (r * z) = (1 / (2 * œÄ)) ‚Ä¢ ‚à´ t in 0..2 * œÄ,
              (R * exp (t * I) / (R * exp (t * I) - z)) ‚Ä¢ f (r * R * exp (t * I)) +
              (star z / (R * star (exp (t * I)) - star z)) ‚Ä¢ f (r * R* exp (t * I)) := by
  rw [intervalIntegral.integral_add]
  ¬∑ rw [cauchy_integral_formula_scaled_disc hR hf hr hz,
        vanishing_goursat_integral_scaled_disc hR hf hr hz, add_zero]
  ¬∑ apply ContinuousOn.intervalIntegrable
    refine ContinuousOn.smul ?_ ?_
    ¬∑ exact ContinuousOn.div (Continuous.continuousOn (by fun_prop))
                               (Continuous.continuousOn (by fun_prop))
                               (fun t _ => neq_in_disc_of_mul_exp_ofReal_mul_I hR hz t)
    ¬∑ exact hf.continuousOn.comp (Continuous.continuousOn (by fun_prop))
              (fun t _ => mem_disc_of_scaled_exp_ofReal_mul_I hR hr t)
  ¬∑ apply ContinuousOn.intervalIntegrable
    refine ContinuousOn.smul ?_ ?_
    ¬∑ exact ContinuousOn.div (Continuous.continuousOn continuous_const)
       (Continuous.continuousOn (by fun_prop))
       (fun t _ => neq_in_disc_of_mul_star_exp_ofReal_mul_I hR hz t)
    ¬∑ exact hf.continuousOn.comp (by fun_prop)
                (fun t _ => mem_disc_of_scaled_exp_ofReal_mul_I hR hr t)

/-- For a ‚ÑÇ-differentiable function `f : ‚ÑÇ ‚Üí E` on a disc centered at `0`, `f(r*z)` equals the
integral of `f(r*R*e^{it})` against the Poisson kernel, where `r ‚àà (0,1)` and `z` is in the disc. -/
theorem poisson_integral_of_differentiableOn_scaled_disc {E : Type*} [NormedAddCommGroup E]
    [NormedSpace ‚ÑÇ E] [CompleteSpace E] {f : ‚ÑÇ ‚Üí E} {z : ‚ÑÇ} {r R : ‚Ñù} (hR : 0 < R)
    (hf : DifferentiableOn ‚ÑÇ f (ball 0 R)) (hr : r ‚àà Ioo 0 1) (hz : z ‚àà ball 0 R) :
    f (r * z) = (1 / (2 * œÄ)) ‚Ä¢ ‚à´ t in 0..2 * œÄ,
      ((R ^ 2 - ‚Äñz‚Äñ ^ 2) / ‚ÄñR * exp (t * I) - z‚Äñ ^ 2) ‚Ä¢ f (r * R * exp (t * I)) := by
  convert cauchy_goursat_integral_scaled_disc hR hf hr hz using 3
  ext t
  rw [‚Üê add_smul]
  apply congrArg (fun (x : ‚ÑÇ) => x ‚Ä¢ f (r * R * exp (t * I)))
  dsimp
  simp only [‚Üê star_def]
  rw [div_add_div _ _ (neq_in_disc_of_mul_exp_ofReal_mul_I hR hz t)
                      (neq_in_disc_of_mul_star_exp_ofReal_mul_I hR hz t)]
  symm
  have : (R * exp (t * I) * (R * star (exp (t * I)) - star z) + (R * cexp (t * I) - z) * star z) =
          R ^ 2 - ‚Äñz‚Äñ ^ 2 := by
    ring_nf
    rw [mul_assoc, mul_comm (star z), star_def]
    simp [mul_conj, normSq_eq_norm_sq]
  rw [this]
  simp only [ofReal_div, ofReal_sub, ofReal_pow]
  congr 1
  have : R * star (exp (t * I)) - star z = star (R * exp (t * I) - z) := by
    simp [star_sub, star_mul']
  rw [this, star_def, mul_conj, normSq_eq_norm_sq, ofReal_pow]


open InnerProductSpace

/-- For a harmonic function `u` on a disc with radius `R`, centered at `0`,
`u(r*z)` equals the integral of `u(r*R*e^{it})` against the Poisson kernel,
where `r ‚àà (0,1)` and `z` is in the disc. -/
theorem poisson_integral_of_harmonicOn_scaled_disc {u : ‚ÑÇ ‚Üí ‚Ñù} {z : ‚ÑÇ} {r R : ‚Ñù}
    (hR : 0 < R) (hu : HarmonicOnNhd u (ball 0 R))
    (hr : r ‚àà Ioo 0 1) (hz : z ‚àà ball 0 R) :
    u (r * z) = (1 / (2 * œÄ)) * ‚à´ t in (0)..(2 * œÄ),
      ((R ^ 2 - ‚Äñz‚Äñ ^ 2) / ‚ÄñR * exp (t * I) - z‚Äñ ^ 2) * u (r * R * exp (t * I)) := by
  have hfu : ‚àÉ (f : ‚ÑÇ ‚Üí ‚ÑÇ), DifferentiableOn ‚ÑÇ f (ball 0 R) ‚àß
    EqOn (fun (z : ‚ÑÇ) => (f z).re) u (ball 0 R) := by
    obtain ‚ü®f, hf‚ü© := @harmonic_is_realOfHolomorphic u 0 R hu
    use f
    exact ‚ü®hf.1.differentiableOn, hf.2‚ü©
  obtain ‚ü®f, hf, hf_eq‚ü© := hfu
  rw [‚Üê hf_eq (mem_disc_of_scaled hR (LT.lt.le (mem_ball_zero_iff.mp hz)) hr)]
  -- We replace `u(rz)` by `Re(f(rz))`.
  have hrt_eq : EqOn
    (fun t : ‚Ñù => (R ^ 2 - ‚Äñz‚Äñ^2) / ‚ÄñR * exp (t * I) - z‚Äñ^2 * (f (r * R * exp (t * I))).re)
    (fun t : ‚Ñù => (R ^ 2 - ‚Äñz‚Äñ^2) / ‚ÄñR * exp (t * I) - z‚Äñ^2 * u (r * R * exp (t * I)))
    (uIcc 0 (2 * œÄ)) :=
       fun t _ => by simp only [‚Üê hf_eq (mem_disc_of_scaled_exp_ofReal_mul_I hR hr t)]
  rw [‚Üê intervalIntegral.integral_congr hrt_eq]
  dsimp
  rw [congr_arg re (poisson_integral_of_differentiableOn_scaled_disc hR hf hr hz),
      smul_re, smul_eq_mul]
  congr 1
  simp only [intervalIntegral.integral_of_le two_pi_pos.le]
  symm
  rw [‚Üê RCLike.re_eq_complex_re]
  convert integral_re _ using 1
  ¬∑ simp only [real_smul, RCLike.mul_re, RCLike.re_to_complex, ofReal_re, RCLike.im_to_complex,
               ofReal_im, zero_mul, sub_zero]
  ¬∑ refine ContinuousOn.integrableOn_Icc ?_ |> fun h => h.mono_set <| Ioc_subset_Icc_self
    refine ContinuousOn.smul ?_ ?_
    ¬∑ exact Continuous.continuousOn (Continuous.div (by fun_prop) (by fun_prop)
              (fun t => by positivity [neq_in_disc_of_mul_exp_ofReal_mul_I hR hz t]))
    ¬∑ exact hf.continuousOn.comp (Continuous.continuousOn (by fun_prop))
                                 (fun t _ => mem_disc_of_scaled_exp_ofReal_mul_I hR hr t)

open Filter Topology

/-- We bound  `t ‚Ü¶ ‚Äñk (R * exp (t * I)) ‚Ä¢ f (r * R * exp (t * I))‚Äñ`, for
`k` continuous on the circle of radius `R` and center `0`,
and `f` continuous on the closed disc of radius `R` and center `0`. -/
lemma bounds_of_continuousOn_circle_closedDisc {E : Type*} [NormedAddCommGroup E]
    [NormedSpace ‚Ñù E] {f : ‚ÑÇ ‚Üí E} {k : ‚ÑÇ ‚Üí ‚Ñù} {r t R : ‚Ñù} (hR : 0 < R) (hr : r ‚àà Ioo 0 1)
    (hf : ContinuousOn f (closedBall 0 R)) (hk : ContinuousOn k (sphere 0 R)) :
    ‚Äñk (R * exp (t * I)) ‚Ä¢ f (r * R * exp (t * I))‚Äñ ‚â§
    sSup ((fun Œ∂ ‚Ü¶ |k Œ∂|) '' sphere 0 R) * sSup ((fun w ‚Ü¶ ‚Äñf w‚Äñ) '' closedBall 0 R) := by
  have h_bds :
      ‚Äñf (r * R * exp (t * I))‚Äñ ‚â§ sSup (image (fun w => ‚Äñf w‚Äñ) (closedBall 0 R)) ‚àß
      ‚Äñk (R * exp (t * I))‚Äñ ‚â§ sSup (image (fun w => ‚Äñk w‚Äñ) (sphere 0 R)) := by
    refine ‚ü®le_csSup ?_ ?_, le_csSup ?_ ?_‚ü©
    ¬∑ exact IsCompact.bddAbove (isCompact_closedBall 0 R |>.image_of_continuousOn hf.norm)
    ¬∑ exact ‚ü®_, ball_subset_closedBall (mem_disc_of_scaled_exp_ofReal_mul_I hR hr t), rfl‚ü©
    ¬∑ exact IsCompact.bddAbove (IsCompact.image_of_continuousOn (isCompact_sphere 0 R) hk.norm)
    ¬∑ exact ‚ü®R * exp (t * I), ‚ü®by simp [norm_exp_ofReal_mul_I, hR.le], rfl‚ü©‚ü©
  have hmul_bds : |k (R * exp (t * I))| * ‚Äñf (r * R * exp (t * I))‚Äñ ‚â§
    (sSup (image (fun Œ∂ => |k Œ∂|) (sphere 0 R))) *
    (sSup (image (fun w => ‚Äñf w‚Äñ) (closedBall 0 R))) := by
        apply mul_le_mul h_bds.2 h_bds.1 (norm_nonneg (f (r * R * exp (t * I))))
        apply sSup_nonneg
        rintro _ ‚ü®_, ‚ü®_, hx‚ü©‚ü©
        simp_rw [‚Üê hx, norm_nonneg]
  have hmul_norm : ‚Äñk (R * exp (t * I)) ‚Ä¢ f (r * R * exp (t * I))‚Äñ ‚â§
    ‚Äñk (R * exp (t * I))‚Äñ * ‚Äñf (r * R * exp (t * I))‚Äñ := by rw [norm_smul]
  exact le_trans hmul_norm hmul_bds

/-- For a sequence `r‚Çô ‚Üí 1` with `r‚Çô ‚àà (0,1)`, the integral of
`t ‚Ü¶ k(R*e^{it}) ‚Ä¢ f(r‚Çô*R*e^{it})` on `[0 , 2œÄ]` converges to the integral of
`t ‚Ü¶ k(R*e^{it}) ‚Ä¢ f(R*e^{it})` on `[0 , 2œÄ]`, when `f` is continuous on the
closed disc of radius `R` and center `0`, and `k` is continuous on the circle of radius `R`
and center `0`, by Lebesgue's Dominated Convergence Theorem. -/
lemma tendsto_integral_prod_of_continuousOn_circle_closedDisc
    {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E]
    {f : ‚ÑÇ ‚Üí E} {k : ‚ÑÇ ‚Üí ‚Ñù} {r : ‚Ñï ‚Üí ‚Ñù} {R : ‚Ñù} (hR : 0 < R)
    (hf : ContinuousOn f (closedBall 0 R)) (hk : ContinuousOn k (sphere 0 R))
    (hr : ‚àÄ n, r n ‚àà Ioo 0 1) (hr_lim : Tendsto r atTop (ùìù 1)) :
    Tendsto (fun n => ‚à´ t in 0..2 * œÄ, k (R * exp (t * I)) ‚Ä¢ f (r n * R * exp (t * I)))
      atTop (ùìù (‚à´ t in 0..2 * œÄ, k (R * exp (t * I)) ‚Ä¢ f (R * exp (t * I)))) := by
  have hrn (n : ‚Ñï) (t : ‚Ñù) : r n * R * exp (t * I) ‚àà closedBall 0 R :=
      ball_subset_closedBall (mem_disc_of_scaled_exp_ofReal_mul_I hR (hr n) t)
  apply intervalIntegral.tendsto_integral_filter_of_dominated_convergence
  rotate_right
  -- We define the bound to be the supremum of the integrand.
  ¬∑ exact fun x => sSup ((fun Œ∂ ‚Ü¶ |k Œ∂|) '' sphere 0 R) * sSup ((fun w ‚Ü¶ ‚Äñf w‚Äñ) '' closedBall 0 R)
  -- We verify the measurability of the integrand.
  ¬∑ apply Eventually.of_forall
    intro n
    apply Continuous.aestronglyMeasurable
    refine Continuous.smul ?_ ?_
    ¬∑ refine ContinuousOn.comp_continuous (s:= sphere 0 R) hk (by fun_prop) ?_
      ¬∑ intro x
        simp [norm_exp_ofReal_mul_I, norm_real, norm_eq_abs, abs_of_pos hR]
    ¬∑ exact ContinuousOn.comp_continuous (s:= closedBall 0 R) hf (by fun_prop) (hrn n)
  -- We verify that the integrand is eventually bounded by the bound.
  ¬∑ exact Eventually.of_forall fun n => Eventually.of_forall fun t ht =>
             bounds_of_continuousOn_circle_closedDisc hR (hr n) hf hk
  ¬∑ simp only [ne_eq, enorm_ne_top, not_false_eq_true, intervalIntegrable_const]
  -- We verify the pointwise convergence of the integrand.
  ¬∑ refine Eventually.of_forall fun x hx => Tendsto.smul tendsto_const_nhds ?_
    apply Tendsto.comp (hf.continuousWithinAt _)
    ¬∑ rw [tendsto_nhdsWithin_iff]
      constructor
      ¬∑ simpa [mul_assoc] using Tendsto.mul
          (continuous_ofReal.continuousAt.tendsto.comp hr_lim) tendsto_const_nhds
      ¬∑ exact Eventually.of_forall (fun n => hrn n x)
    ¬∑ simp [mem_closedBall, dist_zero_right, norm_exp_ofReal_mul_I, abs_of_pos hR]

/-- The Poisson kernel is continuous on the circle. -/
theorem poisson_kernel_continousOn_circle {z : ‚ÑÇ} {R : ‚Ñù} (hz : z ‚àà ball 0 R) :
     ContinuousOn (fun Œ∂ => (R ^ 2 - ‚Äñz‚Äñ ^ 2) / ‚ÄñŒ∂ - z‚Äñ ^ 2) (sphere 0 R) := by
  refine continuousOn_of_forall_continuousAt ?_
  intro Œ∂ hŒ∂
  refine ContinuousAt.div (continuousAt_const) (by fun_prop) ?_
  intro h
  rw [sq_eq_zero_iff, norm_eq_zero, sub_eq_zero] at h
  rw [h, mem_sphere, dist_zero_right] at hŒ∂
  simp [mem_ball, dist_zero_right, hŒ∂] at hz


/-- The sequence `r‚Çô = 1 - 1 / (n + 2)` is in `(0,1)` and tends to `1` as `n ‚Üí ‚àû`. -/
lemma seq_tendsto_to_one_in_unit_interval_aux :
    let r : ‚Ñï ‚Üí ‚Ñù := fun n => 1 - 1 / (n + 2)
    (‚àÄ n, r n ‚àà Ioo 0 1) ‚àß Tendsto r atTop (ùìù 1) := by
  let r : ‚Ñï ‚Üí ‚Ñù := fun n => 1 - 1 / (n + 2)
  have hr (n : ‚Ñï) : r n ‚àà Ioo 0 1 := by
    simp only [one_div, mem_Ioo, sub_pos, sub_lt_self_iff, inv_pos, r]
    have : (1 : ‚Ñù) < n + 2 := by linarith
    exact ‚ü®inv_lt_one_of_one_lt‚ÇÄ this, by linarith‚ü©
  have hr_lim : Tendsto r atTop (ùìù 1) :=
    le_trans (tendsto_const_nhds.sub <| tendsto_const_nhds.div_atTop
      <| tendsto_atTop_add_const_right _ _ tendsto_natCast_atTop_atTop) (by rw [sub_zero])
  exact ‚ü®hr, hr_lim‚ü©

/-- If `r‚Çô` tends to `1`, then `f (r‚Çô * z)` tends to `f z`, for `z` in a disc centered at `0`,
when `f` is continuous on the closed disc. -/
lemma tendsto_of_radius_tendsto_one_of_continuousOn_closedDisc
    {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E] [CompleteSpace E]
    {f : ‚ÑÇ ‚Üí E} {z : ‚ÑÇ} {r : ‚Ñï ‚Üí ‚Ñù} {R : ‚Ñù}
    (hc : ContinuousOn f (closedBall 0 R)) (hr_lim : Tendsto r atTop (ùìù 1))
    (hz : z ‚àà ball 0 R) : Tendsto (fun n => f (r n * z)) atTop (ùìù (f z)) := by
  have h_seq : Tendsto (fun n => r n * z) atTop (ùìù z) := by
    simpa using Tendsto.mul (continuous_ofReal.continuousAt.tendsto.comp hr_lim)
      (tendsto_const_nhds (x := z))
  specialize hc z (ball_subset_closedBall hz)
  have hc : ContinuousAt f z := ContinuousWithinAt.continuousAt hc (closedBall_mem_nhds_of_mem hz)
  exact (ContinuousAt.tendsto hc).comp h_seq

/-- **Poisson integral formula for harmonic functions on a disc**:
A function `u` harmonic on a disc with radius `R` and center `0`,
and continuous on the closed disc, satisfies
`u(z) = (1/2œÄ) ‚à´_0^{2œÄ} (R¬≤ - |z|¬≤) / |R*e^{it} - z|¬≤ u(R*e^{it}) dt` for `z` in the disc. -/
theorem poisson_integral_of_harmonicOn_disc_continuousOn_closedDisc
    {u : ‚ÑÇ ‚Üí ‚Ñù} {z : ‚ÑÇ} {R : ‚Ñù} (hR : 0 < R)
    (hu : HarmonicOnNhd u (ball 0 R)) (hc : ContinuousOn u (closedBall 0 R))
    (hz : z ‚àà ball 0 R) :
    u z = (1 / (2 * œÄ)) * ‚à´ t in 0..(2 * œÄ),
      (R ^ 2 - ‚Äñz‚Äñ ^ 2) / ‚ÄñR * exp (t * I) - z‚Äñ ^ 2 * u (R * exp (t * I)) := by
  let r : ‚Ñï ‚Üí ‚Ñù := fun n => 1 - 1 / (n + 2)
  -- We approximate `1` by a sequence `r‚Çô` in `(0,1)`.
  obtain ‚ü®hr, hr_lim‚ü© := seq_tendsto_to_one_in_unit_interval_aux
  have h_poisson (n : ‚Ñï) := poisson_integral_of_harmonicOn_scaled_disc hR hu (hr n) hz
  have hu_lim := tendsto_integral_prod_of_continuousOn_circle_closedDisc hR hc
                 (poisson_kernel_continousOn_circle hz) hr hr_lim
  have hu_lim : Tendsto (fun n => (u (r n * z))) atTop (ùìù ((1 / (2 * œÄ)) * ‚à´ t in 0..2 * œÄ,
      ((R ^ 2 - ‚Äñz‚Äñ^2) / ‚ÄñR * exp (t * I) - z‚Äñ^2 * u (R * exp (t * I))))) := by
    simp only [r, h_poisson]
    dsimp only [smul_eq_mul] at hu_lim
    exact (Tendsto.const_mul (1 / (2 * œÄ)) hu_lim)
  -- We conclude by uniqueness of limits and by revealing the Poisson kernel.
  rw [‚Üê tendsto_nhds_unique hu_lim
        (tendsto_of_radius_tendsto_one_of_continuousOn_closedDisc hc hr_lim hz)]

/-- **Poisson integral formula for ‚ÑÇ-differentiable functions on a disc**:
A function `f : ‚ÑÇ ‚Üí E` `‚ÑÇ`-differentiable on a disc with radius `R` and center `0`,
and continuous on the closed disc, satisfies
`f(z) = (1/2œÄ) ‚à´_0^{2œÄ} (R¬≤ - |z|¬≤) / |R*e^{it} - z|¬≤ f(R*e^{it}) dt` for `z` in the disc. -/
theorem poisson_integral_of_diffContOnCl_disc
    {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚ÑÇ E] [CompleteSpace E]
    {f : ‚ÑÇ ‚Üí E} {z : ‚ÑÇ} {R : ‚Ñù} (hR : 0 < R)
    (hf : DiffContOnCl ‚ÑÇ f (ball 0 R)) (hz : z ‚àà ball 0 R) :
    f z = (1 / (2 * œÄ)) ‚Ä¢ ‚à´ t in 0..(2 * œÄ),
      ((R ^ 2 - ‚Äñz‚Äñ ^ 2) / ‚ÄñR * exp (t * I) - z‚Äñ ^ 2) ‚Ä¢ f (R * exp (t * I)) := by
  let r : ‚Ñï ‚Üí ‚Ñù := fun n => 1 - 1 / (n + 2)
  obtain ‚ü®hr, hr_lim‚ü© := seq_tendsto_to_one_in_unit_interval_aux
  -- We express `f(r‚Çô z)` as the Poisson integral and then take the limit.
  have h_poisson (n : ‚Ñï) :=
      poisson_integral_of_differentiableOn_scaled_disc hR hf.differentiableOn (hr n) hz
  have hc := DiffContOnCl.continuousOn_ball hf
  have hu_lim : Tendsto (fun n => (f (r n * z))) atTop (ùìù ((1 / (2 * œÄ)) ‚Ä¢ ‚à´ t in 0..2 * œÄ,
      ((R ^ 2 - ‚Äñz‚Äñ ^ 2) / ‚ÄñR * exp (t * I) - z‚Äñ ^ 2) ‚Ä¢ f (R * exp (t * I)))) := by
    simp only [r, h_poisson]
    exact (Tendsto.const_smul (tendsto_integral_prod_of_continuousOn_circle_closedDisc hR hc
            (poisson_kernel_continousOn_circle hz) hr hr_lim) (1 / (2 * œÄ)))
  -- We conclude by uniqueness of limits.
  rw [‚Üê tendsto_nhds_unique (hu_lim)
        (tendsto_of_radius_tendsto_one_of_continuousOn_closedDisc hc hr_lim hz)]

/-- The real part of the Herglotz‚ÄìRiesz kernel is equal to the Poisson kernel. -/
theorem realPart_herglotz_kernel_eq_poisson_kernel {R : ‚Ñù} (x w : ‚ÑÇ) (hx : ‚Äñx‚Äñ = R) :
    ((x + w) / (x - w)).re = (R ^ 2 - ‚Äñw‚Äñ ^ 2) / ‚Äñx - w‚Äñ ^ 2 := by
  rw [div_re, normSq_eq_norm_sq (x - w)]
  calc (x + w).re * (x - w).re / ‚Äñx - w‚Äñ ^ 2 + (x + w).im * (x - w).im / ‚Äñx - w‚Äñ ^ 2
   _ = ((x.re + w.re) * (x.re - w.re) + (x.im + w.im) * (x.im - w.im)) / ‚Äñx - w‚Äñ ^ 2 := by
        simp only [add_re, sub_re, add_im, sub_im, add_div]
   _ = ((x.re * x.re + x.im * x.im) - (w.re * w.re + w.im * w.im)) / ‚Äñx - w‚Äñ ^ 2 := by ring_nf
   _ = ((normSq x) - (normSq w)) / ‚Äñx - w‚Äñ ^ 2 := by simp only [normSq_apply]
   _ = (‚Äñx‚Äñ ^ 2 - ‚Äñw‚Äñ ^ 2) / ‚Äñx - w‚Äñ ^ 2 := by simp only [normSq_eq_norm_sq]
   _ = (R ^ 2 - ‚Äñw‚Äñ ^ 2) / ‚Äñx - w‚Äñ ^ 2 := by rw [hx, pow_two]

/-- **Poisson integral formula for harmonic functions on a disc**:
A function `u : ‚ÑÇ ‚Üí ‚Ñù` harmonic on a disc with radius `R` and center `0`, and
continuous on the closed disc, satisfies
`u(z) = (1/2œÄ) ‚à´_0^{2œÄ} Re((R*e^{it} + z) / (R*e^{it} - z)) * u(R*e^{it}) dt`
for `z` in the disc. -/
theorem poisson_integral_of_harmonicOn_disc_continuousOn_closedDisc_ker_re
    {u : ‚ÑÇ ‚Üí ‚Ñù} {z : ‚ÑÇ} {R : ‚Ñù} (hR : 0 < R)
    (hu : HarmonicOnNhd u (ball 0 R)) (hc : ContinuousOn u (closedBall 0 R))
    (hz : z ‚àà ball 0 R) :
    u z = (1 / (2 * œÄ)) * ‚à´ t in 0..(2 * œÄ),
      ((R * exp (t * I) + z) / (R * exp (t * I) - z)).re * u (R * exp (t * I)) := by
  rw [poisson_integral_of_harmonicOn_disc_continuousOn_closedDisc hR hu hc hz]
  congr 3
  ext t
  congr 1
  exact (realPart_herglotz_kernel_eq_poisson_kernel
         (R * exp (t * I)) z (by simp [norm_exp_ofReal_mul_I, hR.le])).symm

/-- **Poisson integral formula for ‚ÑÇ-differentiable functions on a disc**:
A function `f : ‚ÑÇ ‚Üí E` `‚ÑÇ`-differentiable on a disc with radius `R` and center `0`,
and continuous on the closed disc, satisfies
`f(z) = (1/2œÄ) ‚à´_0^{2œÄ} Re((R*e^{it} + z) / (R*e^{it} - z)) ‚Ä¢ f(R*e^{it}) dt`
for `z` in the disc. -/
theorem poisson_integral_of_diffContOnCl_disc_ker_re
    {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚ÑÇ E] [CompleteSpace E] {f : ‚ÑÇ ‚Üí E} {z : ‚ÑÇ}
    {R : ‚Ñù} (hR : 0 < R) (hf : DiffContOnCl ‚ÑÇ f (ball 0 R)) (hz : z ‚àà ball 0 R) :
    f z = (1 / (2 * œÄ)) ‚Ä¢ ‚à´ t in 0..(2 * œÄ),
      ((R * exp (t * I) + z) / (R * exp (t * I) - z)).re ‚Ä¢ f (R * exp (t * I)) := by
  rw [poisson_integral_of_diffContOnCl_disc hR hf hz]
  congr 3
  ext t
  congr 1
  exact (realPart_herglotz_kernel_eq_poisson_kernel
         (R * exp (t * I)) z (by simp [norm_exp_ofReal_mul_I, hR.le])).symm

/-- **Poisson integral formula for harmonic functions on a disc**:
A function `u : ‚ÑÇ ‚Üí ‚Ñù` harmonic on a disc with radius `R` and center `0`,
and continuous on the closed disc, satisfies
`u(z) = (1/2œÄ) ‚à´_0^{2œÄ} Re((R*e^{it} + z) / (R*e^{it} - z)) * u(R*e^{it}) dt`
for `z` in the disc. -/
theorem circleAverage_of_harmonicOn_disc_continuousOn_closedDisc
    {u : ‚ÑÇ ‚Üí ‚Ñù} {z : ‚ÑÇ} {R : ‚Ñù} (hR : 0 < R)
    (hu : HarmonicOnNhd u (ball 0 R)) (hc : ContinuousOn u (closedBall 0 R)) (hz : z ‚àà ball 0 R) :
    u z = circleAverage (fun Œ∂ => ((Œ∂ + z) / (Œ∂ - z)).re * u (Œ∂)) 0 R := by
  simp [circleAverage, circleMap,
        poisson_integral_of_harmonicOn_disc_continuousOn_closedDisc_ker_re hR hu hc hz]

/-- **Poisson integral formula for ‚ÑÇ-differentiable functions on a disc**:
A function `f : ‚ÑÇ ‚Üí E` `‚ÑÇ`-differentiable on a disc with radius `R` and center `0`,
and continuous on the closed disc, satisfies
`f(z) = (1/2œÄ) ‚à´_0^{2œÄ} Re((R*e^{it} + z) / (R*e^{it} - z)) ‚Ä¢ f(R*e^{it}) dt`
for `z` in the disc. -/
theorem circleAverage_of_diffContOnCl_disc_ker_re
    {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚ÑÇ E] [CompleteSpace E]
    {f : ‚ÑÇ ‚Üí E} {z : ‚ÑÇ} {R : ‚Ñù} (hR : 0 < R)
    (hf : DiffContOnCl ‚ÑÇ f (ball 0 R)) (hz : z ‚àà ball 0 R) :
    f z = circleAverage (fun Œ∂ => ((Œ∂ + z) / (Œ∂ - z)).re ‚Ä¢ f (Œ∂)) 0 R := by
  simp [circleAverage, circleMap,
        poisson_integral_of_diffContOnCl_disc_ker_re hR hf hz]
