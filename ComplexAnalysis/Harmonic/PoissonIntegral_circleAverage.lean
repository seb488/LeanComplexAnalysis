/-
Copyright (c) 2025 [Your Name]. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: [Your Name], [Collaborator Name if applicable]
-/
module
public import Mathlib.Analysis.Complex.Harmonic.Analytic
public import Mathlib.Analysis.Complex.MeanValue
public import Mathlib.MeasureTheory.Integral.CircleAverage

/-!
# The Poisson Integral Formula on Disc

## Main results

Theorems `poisson_integral_of_harmonicOn_disc_continuousOn_closedDisc` and
`poisson_integral_of_harmonicOn_disc_continuousOn_closedDisc_ker_re`:
Every function `u : ‚ÑÇ ‚Üí ‚Ñù` harmonic on the disc with radius `R` and center `0`, and
continuous on the closed disc, can be represented as
```
u(z) = 1/(2œÄ) ‚à´_0^{2œÄ} (R¬≤ - |z|¬≤) / |R * exp (it) - z|¬≤ * u(R * exp (it)) dt
     = 1/(2œÄ) ‚à´_0^{2œÄ} Re((R * exp (it) + z) / (R * exp (it) - z)) * u(R * exp (it)) dt,
```
for `z` in the disc.

Theorem `poisson_integral_of_diffContOnCl_disc` and `poisson_integral_of_diffContOnCl_disc_ker_re`:
Every function `f : ‚ÑÇ ‚Üí E` ‚ÑÇ-differentiable on the disc with radius `R` and center `0`, and
continuous on the closed disc, with values in a complex Banach space `E`, can be represented as
```
f(z) = 1/(2œÄ) ‚à´_0^{2œÄ} (R¬≤ - |z|¬≤) / |R * exp (it) - z|¬≤ ‚Ä¢ f(R * exp (it)) dt
     = 1/(2œÄ) ‚à´_0^{2œÄ} Re((R * exp (it) + z) / (R * exp (it) - z)) ‚Ä¢ f(R * exp (it)) dt,
```
for `z` in the disc.

## Implementation Notes

The proof follows from
- Cauchy Integral Formula,
- Cauchy-Goursat Theorem,
- a harmonic function is the real part of a holomorphic function on a disc,
- Lebesgue's Dominated Convergence Theorem.

## References

[Rudin, *Real and Complex Analysis* (Theorem 11.9)][rudin2006real]

## Tags

circleAverage, ‚ÑÇ-differentiable function, harmonic function, Poisson integral.
-/

public section

open Complex Metric Real Set

/-!
## Preliminary lemmata

The lemmata in this section are used in the proofs of the main results.
They are stated and proved separately to avoid cluttering the main proofs.
-/

/-- Scaling by `r ‚àà (0,1)` a point in a closed disc centerd at `0` is in the open disc. -/
lemma mem_disc_of_scaled {z : ‚ÑÇ} {r R : ‚Ñù} (hR : 0 < R)
    (hz : ‚Äñz‚Äñ ‚â§ R) (hr : r ‚àà Ioo 0 1) : r * z ‚àà ball 0 R := by
  rw [mem_ball, dist_zero_right, norm_mul, norm_real, norm_eq_abs, abs_of_pos hr.1]
  exact lt_of_le_of_lt (mul_le_mul_of_nonneg_left hz (le_of_lt hr.1))
                       ((mul_lt_iff_lt_one_left hR).mpr hr.2)

/-- `r * R * exp (t * I)` is in the disc of radius `R` and center `0`, for `r ‚àà (0,1)`. -/
lemma mem_disc_of_scaled_exp_ofReal_mul_I {r R : ‚Ñù} (hR : 0 < R) (hr : r ‚àà Ioo 0 1) (t : ‚Ñù) :
    r * R * exp (t * I) ‚àà ball 0 R := by
      rw [mul_assoc]
      apply mem_disc_of_scaled hR _ hr
      simp [norm_exp_ofReal_mul_I, abs_of_pos hR]

/-- `R * exp (t * I)` is not equal to any `z` in the disc of radius `R`, centered at `0`. -/
lemma neq_in_disc_of_mul_exp_ofReal_mul_I {z : ‚ÑÇ} {R : ‚Ñù}
    (hz : z ‚àà ball 0 R) (t : ‚Ñù) : R * exp (t * I) - z ‚â† 0 := by
  intro h
  rw [sub_eq_zero] at h
  simp [‚Üê h, mem_ball, dist_zero_right, norm_exp_ofReal_mul_I,
        norm_real, norm_eq_abs, abs_of_pos (pos_of_mem_ball hz)] at hz

/-- `R * star (exp (t * I))` is not equal to any `z` in the disc of radius `R`, centered at `0`. -/
lemma neq_in_disc_of_mul_star_exp_ofReal_mul_I {z : ‚ÑÇ} {R : ‚Ñù}
    (hz : z ‚àà ball 0 R) (t : ‚Ñù) : star (R * exp (t * I)) - star z ‚â† 0 := by
  rw [‚Üê star_sub]
  exact star_ne_zero.mpr (neq_in_disc_of_mul_exp_ofReal_mul_I hz t)

/-- `R ^ 2 - star z * w ‚â† 0`, for `z` in the disc with radius `R` and center `0`,
and for `w` in the closed disc. -/
lemma radius_sq_sub_star_mul_neq_zero {z : ‚ÑÇ} {w : ‚ÑÇ} {R : ‚Ñù}
    (hz : z ‚àà ball 0 R) (hw : w ‚àà closedBall 0 R) : R ^ 2 - star z * w ‚â† 0 := by
  intro h
  have hz_norm : ‚Äñz‚Äñ < R := by rw [mem_ball_zero_iff] at hz; exact hz
  have hw_norm : ‚Äñw‚Äñ ‚â§ R := mem_closedBall_zero_iff.mp hw
  have : ‚Äñstar z * w‚Äñ < R ^ 2 := by
    calc ‚Äñstar z * w‚Äñ ‚â§ ‚Äñstar z‚Äñ * ‚Äñw‚Äñ := norm_mul_le _ _
      _ = ‚Äñz‚Äñ * ‚Äñw‚Äñ := by rw [norm_star]
      _ < R ^ 2 := by  nlinarith [norm_nonneg z, norm_nonneg w]
  rw [sub_eq_zero] at h
  rw [‚Üê h] at this
  simp [norm_real, norm_eq_abs, abs_of_pos (pos_of_mem_ball hz)] at this

/-- The real part of the Herglotz‚ÄìRiesz kernel is equal to the Poisson kernel. -/
lemma realPart_herglotz_ker_eq_poisson_ker {R : ‚Ñù} (Œ∂ z : ‚ÑÇ) (hŒ∂ : ‚ÄñŒ∂‚Äñ = R) :
    ((Œ∂ + z) / (Œ∂ - z)).re = (R ^ 2 - ‚Äñz‚Äñ ^ 2) / ‚ÄñŒ∂ - z‚Äñ ^ 2 := by
  rw [div_re, normSq_eq_norm_sq (Œ∂ - z)]
  calc (Œ∂ + z).re * (Œ∂ - z).re / ‚ÄñŒ∂ - z‚Äñ ^ 2 + (Œ∂ + z).im * (Œ∂ - z).im / ‚ÄñŒ∂ - z‚Äñ ^ 2
   _ = ((Œ∂.re + z.re) * (Œ∂.re - z.re) + (Œ∂.im + z.im) * (Œ∂.im - z.im)) / ‚ÄñŒ∂ - z‚Äñ ^ 2 := by
        simp only [add_re, sub_re, add_im, sub_im, add_div]
   _ = ((Œ∂.re * Œ∂.re + Œ∂.im * Œ∂.im) - (z.re * z.re + z.im * z.im)) / ‚ÄñŒ∂ - z‚Äñ ^ 2 := by ring_nf
   _ = ((normSq Œ∂) - (normSq z)) / ‚ÄñŒ∂ - z‚Äñ ^ 2 := by simp only [normSq_apply]
   _ = (‚ÄñŒ∂‚Äñ ^ 2 - ‚Äñz‚Äñ ^ 2) / ‚ÄñŒ∂ - z‚Äñ ^ 2 := by simp only [normSq_eq_norm_sq]
   _ = (R ^ 2 - ‚Äñz‚Äñ ^ 2) / ‚ÄñŒ∂ - z‚Äñ ^ 2 := by rw [hŒ∂, pow_two]

/-- The Poisson kernel is continuous on the circle. -/
lemma poisson_ker_continousOn_circle {z : ‚ÑÇ} {R : ‚Ñù} (hz : z ‚àà ball 0 R) :
     ContinuousOn (fun Œ∂ => (R ^ 2 - ‚Äñz‚Äñ ^ 2) / ‚ÄñŒ∂ - z‚Äñ ^ 2) (sphere 0 R) := by
  refine continuousOn_of_forall_continuousAt ?_
  intro Œ∂ hŒ∂
  refine ContinuousAt.div (continuousAt_const) (by fun_prop) ?_
  intro h
  rw [sq_eq_zero_iff, norm_eq_zero, sub_eq_zero] at h
  rw [h, mem_sphere, dist_zero_right] at hŒ∂
  simp [mem_ball, dist_zero_right, hŒ∂] at hz

/-- If `f` is `‚ÑÇ`-differentiable on a disc centered at zero,
then `Œ∂ ‚Ü¶ f (r * Œ∂)` is differentiable at `z` for `r` in `(0,1)` and `z` in the closed disc. -/
lemma differentiableAt_of_differentiableOn_disc_of_mul {E : Type*} [NormedAddCommGroup E]
    [NormedSpace ‚ÑÇ E] {f : ‚ÑÇ ‚Üí E} {z : ‚ÑÇ} {r R : ‚Ñù} (hR : 0 < R)
    (hf : DifferentiableOn ‚ÑÇ f (ball 0 R)) (hz : z ‚àà closedBall 0 R) (hr : r ‚àà Ioo 0 1) :
    DifferentiableAt ‚ÑÇ (fun Œ∂ => f (r * Œ∂)) z := by
  rw [mem_closedBall, dist_zero_right] at hz
  exact DifferentiableAt.comp z (hf.differentiableAt
        (isOpen_ball.mem_nhds (mem_disc_of_scaled hR hz hr)))
        (differentiableAt_id.const_mul _)

/-- If `f` is `‚ÑÇ`-differentiable on a disc centered at zero, then
`Œ∂ ‚Ü¶ (star z / (R ^ 2 - star z * Œ∂)) ‚Ä¢ f (r * Œ∂)` is differentiable at `w`
in the closed disc with radius `R` and center `0`, for `r` in `(0,1)` and `z` in the open disc.
This lemma will be used in the Cauchy-Goursat theorem. -/
lemma differentiableAt_goursat_integrand_scaled_disc
    {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚ÑÇ E] {f : ‚ÑÇ ‚Üí E} {z w : ‚ÑÇ} {r R : ‚Ñù}
    (hf : DifferentiableOn ‚ÑÇ f (ball 0 R)) (hr : r ‚àà Ioo 0 1)
    (hz : z ‚àà ball 0 R) (hw : w ‚àà closedBall 0 R) :
    DifferentiableAt ‚ÑÇ (fun Œ∂ => (star z / (R ^ 2 - star z * Œ∂)) ‚Ä¢ f (r * Œ∂)) w := by
  refine DifferentiableAt.smul ?_ ?_
  ¬∑ refine DifferentiableAt.div (differentiableAt_const _) ?_ ?_
    ¬∑ refine DifferentiableAt.sub (differentiableAt_const (R ^ 2 : ‚ÑÇ)) ?_
      exact DifferentiableAt.mul (differentiableAt_const (star z)) differentiableAt_id
    ¬∑ exact radius_sq_sub_star_mul_neq_zero hz hw
  ¬∑ exact differentiableAt_of_differentiableOn_disc_of_mul (pos_of_mem_ball hz) hf hw hr

/-!
## Poisson Integrals on scaled discs

We apply the Cauchy Integral Formula (Generalized Mean Value Property) and the Cauchy-Goursat
Theorem to compute values of a `‚ÑÇ`-differentiable function with value in a Banach space `E`,
in the interior of a scaled disk, as a Poisson integral. Also, we have the same result for harmonic
functions, by using the fact that a harmonic function on a disc is the real part of a holomorphic
function on the disc.
-/

/-- Cauchy's integral formula for `‚ÑÇ`-differentiable functions on a disc centred at 0,
evaluated at scaled points `r * z` with `r ‚àà (0,1)` and `z` in the open disc. -/
lemma cauchy_integral_formula_scaled_disc {E : Type*} [NormedAddCommGroup E]
    [NormedSpace ‚ÑÇ E] [CompleteSpace E] {f : ‚ÑÇ ‚Üí E} {z : ‚ÑÇ} {r R : ‚Ñù}
    (hf : DifferentiableOn ‚ÑÇ f (ball 0 R)) (hr : r ‚àà Ioo 0 1) (hz : z ‚àà ball 0 R) :
    f (r * z) = circleAverage (fun Œ∂ => (Œ∂ / (Œ∂ - z)) ‚Ä¢ f (r * Œ∂)) 0 R := by
  have hR : |R| = R ‚àß 0 < R := by constructor <;> simp [abs_of_pos, pos_of_mem_ball hz]
  rw [‚Üê hR.1] at hz hf
  have hfr_diff: ‚àÄ Œ∂ ‚àà ball 0 |R| \ ‚àÖ , DifferentiableAt ‚ÑÇ (fun Œ∂ => f (r * Œ∂)) Œ∂ := by
    intro Œ∂ hŒ∂
    simp only [diff_empty] at hŒ∂
    exact differentiableAt_of_differentiableOn_disc_of_mul (pos_of_mem_ball hz) hf
            (ball_subset_closedBall hŒ∂) hr
  have hfr_cont : ContinuousOn (fun Œ∂ => f (r * Œ∂)) (closedBall 0 |R|) := fun x hx =>
     (DifferentiableAt.continuousAt (differentiableAt_of_differentiableOn_disc_of_mul
                  (pos_of_mem_ball hz) hf hx hr)).continuousWithinAt
-- We apply the Cauchy Integral Formula given by the Generalized Mean Value Property.
  rw [‚Üê circleAverage_sub_sub_inv_smul_of_differentiable_on_off_countable
           countable_empty hfr_cont hfr_diff hz hR.2.ne']
  congr 1
  ext Œ∂
  simp [sub_zero]

/-- We apply the Cauchy-Goursat theorem to the function
`Œ∂ ‚Ü¶ (star z / (R ^ 2 - star z * Œ∂)) ‚Ä¢ (f (r * Œ∂)))`
on the circle of radius `R`, centered at `0`, for `r` in `(0,1)` and `z` in the open disc. -/
lemma vanishing_goursat_circleIntegral_scaled_disc
    {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚ÑÇ E] {f : ‚ÑÇ ‚Üí E} {z : ‚ÑÇ} {r R : ‚Ñù}
    (hf : DifferentiableOn ‚ÑÇ f (ball 0 R)) (hr : r ‚àà Ioo 0 1) (hz : z ‚àà ball 0 R) :
    ‚àÆ Œ∂ in C(0, R), (star z / (R ^ 2 - star z * Œ∂)) ‚Ä¢ f (r * Œ∂) = 0 := by
  apply circleIntegral_eq_zero_of_differentiable_on_off_countable
          (pos_of_mem_ball hz).le countable_empty
  ¬∑ exact fun Œ∂ hŒ∂ => (DifferentiableAt.continuousAt
          (differentiableAt_goursat_integrand_scaled_disc hf hr hz hŒ∂)).continuousWithinAt
  ¬∑ rw [diff_empty]
    exact fun Œ∂ hŒ∂ => differentiableAt_goursat_integrand_scaled_disc hf hr hz
                      (ball_subset_closedBall hŒ∂)

/-- The Cauchy-Goursat theorem for a disc centered at `0` implies the integral of a
`‚ÑÇ`-differentiable function against a conjugate Cauchy kernel vanishes. -/
lemma vanishing_goursat_integral_scaled_disc {E : Type*} [NormedAddCommGroup E]
    [NormedSpace ‚ÑÇ E] {f : ‚ÑÇ ‚Üí E} {z : ‚ÑÇ} {r R : ‚Ñù}
    (hf : DifferentiableOn ‚ÑÇ f (ball 0 R)) (hr : r ‚àà Ioo 0 1) (hz : z ‚àà ball 0 R) :
    circleAverage (fun Œ∂ => (star z / (star Œ∂ - star z)) ‚Ä¢ f (r * Œ∂)) 0 R = 0 := by
  rw [circleAverage_eq_circleIntegral (f := (fun Œ∂ => (star z / (star Œ∂ - star z)) ‚Ä¢ f (r * Œ∂)))
            (c := 0) (pos_of_mem_ball hz).ne']
  apply smul_eq_zero.mpr
  right
  rw [‚Üê vanishing_goursat_circleIntegral_scaled_disc hf hr hz]
  refine circleIntegral.integral_congr (pos_of_mem_ball hz).le ?_
  intro Œ∂ hŒ∂
  simp only [smul_smul]
  congr 1
  rw [mem_sphere, dist_zero_right] at hŒ∂
  rw [sub_zero, inv_eq_one_div, div_mul_div_comm, one_mul, mul_sub,
      star_def, mul_conj', hŒ∂, mul_comm Œ∂]

/-- We put together `cauchy_integral_formula_scaled_disc` and
`vanishing_goursat_integral_scaled_disc`. -/
lemma cauchy_goursat_integral_scaled_disc {E : Type*} [NormedAddCommGroup E]
    [NormedSpace ‚ÑÇ E] [CompleteSpace E] {f : ‚ÑÇ ‚Üí E} {z : ‚ÑÇ} {r R : ‚Ñù}
    (hf : DifferentiableOn ‚ÑÇ f (ball 0 R)) (hr : r ‚àà Ioo 0 1) (hz : z ‚àà ball 0 R) :
    f (r * z) = circleAverage ((fun Œ∂ => (Œ∂ / (Œ∂ - z)) ‚Ä¢ f (r * Œ∂)) +
                               (fun Œ∂ => (star z / (star Œ∂ - star z)) ‚Ä¢ f (r * Œ∂))) 0 R := by
  rw [circleAverage_add]
  ¬∑ rw [‚Üê cauchy_integral_formula_scaled_disc hf hr hz,
      vanishing_goursat_integral_scaled_disc hf hr hz, add_zero]
  ¬∑ apply ContinuousOn.intervalIntegrable
    refine ContinuousOn.smul ?_ ?_
    ¬∑ exact ContinuousOn.div (Continuous.continuousOn (by fun_prop))
        (Continuous.continuousOn (by fun_prop))
        (fun t _ => by rw [circleMap, zero_add]; exact neq_in_disc_of_mul_exp_ofReal_mul_I hz t)
    ¬∑ exact hf.continuousOn.comp (Continuous.continuousOn (by fun_prop))
        (fun t _ => by rw [circleMap, zero_add, ‚Üê mul_assoc]
                       exact mem_disc_of_scaled_exp_ofReal_mul_I (pos_of_mem_ball hz) hr t)
  ¬∑ apply ContinuousOn.intervalIntegrable
    refine ContinuousOn.smul ?_ ?_
    ¬∑ exact ContinuousOn.div (Continuous.continuousOn continuous_const)
       (Continuous.continuousOn (by fun_prop))
       (fun t _ => by rw [circleMap, zero_add]; exact neq_in_disc_of_mul_star_exp_ofReal_mul_I hz t)
    ¬∑ exact hf.continuousOn.comp (Continuous.continuousOn (by fun_prop))
        (fun t _ => by rw [circleMap, zero_add, ‚Üê mul_assoc]
                       exact mem_disc_of_scaled_exp_ofReal_mul_I (pos_of_mem_ball hz) hr t)

/-- For a `‚ÑÇ`-differentiable function `f : ‚ÑÇ ‚Üí E` on a disc centered at `0`, with radius `R`,
`f(r*z)` equals the integral of `f(r*R*e^{it})` against the Poisson kernel,
where `r ‚àà (0,1)` and `z` is in the disc. -/
theorem poisson_integral_of_differentiableOn_scaled_disc {E : Type*} [NormedAddCommGroup E]
    [NormedSpace ‚ÑÇ E] [CompleteSpace E] {f : ‚ÑÇ ‚Üí E} {z : ‚ÑÇ} {r R : ‚Ñù}
    (hf : DifferentiableOn ‚ÑÇ f (ball 0 R)) (hr : r ‚àà Ioo 0 1) (hz : z ‚àà ball 0 R) :
    f (r * z) = circleAverage (fun Œ∂ => ((R ^ 2 - ‚Äñz‚Äñ ^ 2) / ‚ÄñŒ∂ - z‚Äñ ^ 2) ‚Ä¢ f (r * Œ∂)) 0 R := by
  rw [cauchy_goursat_integral_scaled_disc hf hr hz]
  refine circleAverage_congr_sphere ?_
  intro Œ∂ hŒ∂
  dsimp
  rw [‚Üê add_smul]
  congr 1
  dsimp
  simp only [‚Üê star_def, ‚Üê star_sub]
  rw [abs_of_pos (pos_of_mem_ball hz), mem_sphere, dist_zero_right] at hŒ∂
  have h_neq : Œ∂ - z ‚â† 0 := by
    intro heq
    rw [sub_eq_zero] at heq
    rw [heq] at hŒ∂
    rw [mem_ball, dist_zero_right, hŒ∂] at hz
    exact (lt_self_iff_false R).mp hz
  rw [div_add_div _ _ h_neq (star_ne_zero.mpr h_neq), star_def, mul_conj,
      normSq_eq_norm_sq, ofReal_pow, ofReal_div, ofReal_pow]
  congr 1
  rw [‚Üê star_def, star_sub, mul_sub, sub_mul, star_def, mul_conj, normSq_eq_norm_sq , hŒ∂,
      mul_conj, normSq_eq_norm_sq, ofReal_sub, ofReal_pow]
  ring_nf

open InnerProductSpace

/-- For a harmonic function `u` on a disc with radius `R`, centered at `0`,
`u(r*z)` equals the integral of `u(r*R*e^{it})` against the Poisson kernel,
where `r ‚àà (0,1)` and `z` is in the disc. -/
theorem poisson_integral_of_harmonicOn_scaled_disc {u : ‚ÑÇ ‚Üí ‚Ñù} {z : ‚ÑÇ} {r R : ‚Ñù}
    (hu : HarmonicOnNhd u (ball 0 R)) (hr : r ‚àà Ioo 0 1) (hz : z ‚àà ball 0 R) :
    u (r * z) = circleAverage (fun Œ∂ => ((R ^ 2 - ‚Äñz‚Äñ ^ 2) / ‚ÄñŒ∂ - z‚Äñ ^ 2) * u (r * Œ∂)) 0 R := by
  have hfu : ‚àÉ (f : ‚ÑÇ ‚Üí ‚ÑÇ), DifferentiableOn ‚ÑÇ f (ball 0 R) ‚àß
    EqOn (fun (z : ‚ÑÇ) => (f z).re) u (ball 0 R) := by
    obtain ‚ü®f, hf‚ü© := @harmonic_is_realOfHolomorphic u 0 R hu
    use f
    exact ‚ü®hf.1.differentiableOn, hf.2‚ü©
  obtain ‚ü®f, hf, hf_eq‚ü© := hfu
  rw [‚Üê hf_eq (mem_disc_of_scaled (pos_of_mem_ball hz) (LT.lt.le (mem_ball_zero_iff.mp hz)) hr)]
  -- We replace `u(rz)` by `Re(f(rz))`.
  have hrt_eq : EqOn (fun Œ∂ => (R ^ 2 - ‚Äñz‚Äñ^2) / ‚ÄñŒ∂ - z‚Äñ^2 * (f (r * Œ∂)).re)
        (fun Œ∂ => (R ^ 2 - ‚Äñz‚Äñ^2) / ‚ÄñŒ∂ - z‚Äñ^2 * u (r * Œ∂)) (sphere 0 |R|) := fun Œ∂ hŒ∂ => by
    rw [abs_of_pos (pos_of_mem_ball hz)] at hŒ∂
    have : ‚ÄñŒ∂‚Äñ ‚â§ R := by rw [mem_sphere, dist_zero_right] at hŒ∂; exact hŒ∂.le
    have : r * Œ∂ ‚àà ball 0 R := mem_disc_of_scaled (pos_of_mem_ball hz) this hr
    simp only [‚Üê hf_eq this]
  rw [‚Üê circleAverage_congr_sphere hrt_eq]
  dsimp
  rw [congr_arg re (poisson_integral_of_differentiableOn_scaled_disc hf hr hz),
      circleAverage, circleAverage, smul_re]
  congr 1
  simp only [intervalIntegral.integral_of_le two_pi_pos.le]
  symm
  rw [‚Üê RCLike.re_eq_complex_re]
  convert integral_re _ using 1
  ¬∑ simp only [real_smul, RCLike.mul_re, RCLike.re_to_complex, ofReal_re, RCLike.im_to_complex,
               ofReal_im, zero_mul, sub_zero]
  ¬∑ refine ContinuousOn.integrableOn_Icc ?_ |> fun h => h.mono_set <| Ioc_subset_Icc_self
    refine ContinuousOn.smul ?_ ?_
    ¬∑ exact Continuous.continuousOn (Continuous.div (by fun_prop) (by fun_prop) (fun t => by
        rw [circleMap, zero_add]; positivity [neq_in_disc_of_mul_exp_ofReal_mul_I hz t]))
    ¬∑ exact hf.continuousOn.comp (Continuous.continuousOn (by fun_prop))
        (fun t _ => by rw [circleMap, zero_add, ‚Üê mul_assoc]
                       exact mem_disc_of_scaled_exp_ofReal_mul_I (pos_of_mem_ball hz) hr t)

/-!
## Some convergence results

In order to extend the Poisson integral formula to the boundary of the disc, we need some
convergence results. The main ingredient is Lebesgue's Dominated Convergence Theorem.
-/

open Filter Topology

/-- We bound  `t ‚Ü¶ ‚Äñk (R * exp (t * I)) ‚Ä¢ f (r * R * exp (t * I))‚Äñ`, for
`k` continuous on the circle of radius `R` and center `0`,
and `f` continuous on the closed disc of radius `R` and center `0`. -/
lemma bounds_of_continuousOn_circle_closedDisc {E : Type*} [NormedAddCommGroup E]
    [NormedSpace ‚Ñù E] {f : ‚ÑÇ ‚Üí E} {k : ‚ÑÇ ‚Üí ‚Ñù} {r t R : ‚Ñù} (hR : 0 < R) (hr : r ‚àà Ioo 0 1)
    (hf : ContinuousOn f (closedBall 0 R)) (hk : ContinuousOn k (sphere 0 R)) :
    ‚Äñk (R * exp (t * I)) ‚Ä¢ f (r * R * exp (t * I))‚Äñ ‚â§
    sSup ((fun Œ∂ ‚Ü¶ |k Œ∂|) '' sphere 0 R) * sSup ((fun w ‚Ü¶ ‚Äñf w‚Äñ) '' closedBall 0 R) := by
  have h_bds :
      ‚Äñf (r * R * exp (t * I))‚Äñ ‚â§ sSup (image (fun w => ‚Äñf w‚Äñ) (closedBall 0 R)) ‚àß
      ‚Äñk (R * exp (t * I))‚Äñ ‚â§ sSup (image (fun w => ‚Äñk w‚Äñ) (sphere 0 R)) := by
    refine ‚ü®le_csSup ?_ ?_, le_csSup ?_ ?_‚ü©
    ¬∑ exact IsCompact.bddAbove (isCompact_closedBall 0 R |>.image_of_continuousOn hf.norm)
    ¬∑ exact ‚ü®_, ball_subset_closedBall (mem_disc_of_scaled_exp_ofReal_mul_I hR hr t), rfl‚ü©
    ¬∑ exact IsCompact.bddAbove (IsCompact.image_of_continuousOn (isCompact_sphere 0 R) hk.norm)
    ¬∑ exact ‚ü®R * exp (t * I), ‚ü®by simp [norm_exp_ofReal_mul_I, hR.le], rfl‚ü©‚ü©
  have hmul_bds : |k (R * exp (t * I))| * ‚Äñf (r * R * exp (t * I))‚Äñ ‚â§
    (sSup (image (fun Œ∂ => |k Œ∂|) (sphere 0 R))) *
    (sSup (image (fun w => ‚Äñf w‚Äñ) (closedBall 0 R))) := by
        apply mul_le_mul h_bds.2 h_bds.1 (norm_nonneg (f (r * R * exp (t * I))))
        apply sSup_nonneg
        rintro _ ‚ü®_, ‚ü®_, hx‚ü©‚ü©
        simp_rw [‚Üê hx, norm_nonneg]
  have hmul_norm : ‚Äñk (R * exp (t * I)) ‚Ä¢ f (r * R * exp (t * I))‚Äñ ‚â§
    ‚Äñk (R * exp (t * I))‚Äñ * ‚Äñf (r * R * exp (t * I))‚Äñ := by rw [norm_smul]
  exact le_trans hmul_norm hmul_bds

/-- For a sequence `r‚Çô ‚Üí 1` with `r‚Çô ‚àà (0,1)`, the integral of
`t ‚Ü¶ k(R*e^{it}) ‚Ä¢ f(r‚Çô*R*e^{it})` on `[0 , 2œÄ]` converges to the integral of
`t ‚Ü¶ k(R*e^{it}) ‚Ä¢ f(R*e^{it})` on `[0 , 2œÄ]`, when `f` is continuous on the
closed disc of radius `R` and center `0`, and `k` is continuous on the circle of radius `R`
and center `0`, by Lebesgue's Dominated Convergence Theorem. -/
lemma tendsto_integral_prod_of_continuousOn_circle_closedDisc
    {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E]
    {f : ‚ÑÇ ‚Üí E} {k : ‚ÑÇ ‚Üí ‚Ñù} {r : ‚Ñï ‚Üí ‚Ñù} {R : ‚Ñù} (hR : 0 < R)
    (hf : ContinuousOn f (closedBall 0 R)) (hk : ContinuousOn k (sphere 0 R))
    (hr : ‚àÄ n, r n ‚àà Ioo 0 1) (hr_lim : Tendsto r atTop (ùìù 1)) :
    Tendsto (fun n => ‚à´ t in 0..2 * œÄ, k (R * exp (t * I)) ‚Ä¢ f (r n * R * exp (t * I)))
      atTop (ùìù (‚à´ t in 0..2 * œÄ, k (R * exp (t * I)) ‚Ä¢ f (R * exp (t * I)))) := by
  have hrn (n : ‚Ñï) (t : ‚Ñù) : r n * R * exp (t * I) ‚àà closedBall 0 R :=
      ball_subset_closedBall (mem_disc_of_scaled_exp_ofReal_mul_I hR (hr n) t)
  apply intervalIntegral.tendsto_integral_filter_of_dominated_convergence
  rotate_right
  -- We define the bound to be the supremum of the integrand.
  ¬∑ exact fun x => sSup ((fun Œ∂ ‚Ü¶ |k Œ∂|) '' sphere 0 R) * sSup ((fun w ‚Ü¶ ‚Äñf w‚Äñ) '' closedBall 0 R)
  -- We verify the measurability of the integrand.
  ¬∑ apply Eventually.of_forall
    intro n
    apply Continuous.aestronglyMeasurable
    refine Continuous.smul ?_ ?_
    ¬∑ refine ContinuousOn.comp_continuous (s:= sphere 0 R) hk (by fun_prop) ?_
      ¬∑ intro x
        simp [norm_exp_ofReal_mul_I, norm_real, norm_eq_abs, abs_of_pos hR]
    ¬∑ exact ContinuousOn.comp_continuous (s:= closedBall 0 R) hf (by fun_prop) (hrn n)
  -- We verify that the integrand is eventually bounded by the bound.
  ¬∑ exact Eventually.of_forall fun n => Eventually.of_forall fun t ht =>
             bounds_of_continuousOn_circle_closedDisc hR (hr n) hf hk
  ¬∑ simp only [ne_eq, enorm_ne_top, not_false_eq_true, intervalIntegrable_const]
  -- We verify the pointwise convergence of the integrand.
  ¬∑ refine Eventually.of_forall fun x hx => Tendsto.smul tendsto_const_nhds ?_
    apply Tendsto.comp (hf.continuousWithinAt _)
    ¬∑ rw [tendsto_nhdsWithin_iff]
      constructor
      ¬∑ simpa [mul_assoc] using Tendsto.mul
          (continuous_ofReal.continuousAt.tendsto.comp hr_lim) tendsto_const_nhds
      ¬∑ exact Eventually.of_forall (fun n => hrn n x)
    ¬∑ simp [mem_closedBall, dist_zero_right, norm_exp_ofReal_mul_I, abs_of_pos hR]

/-- If `r‚Çô` tends to `1`, then `f (r‚Çô * z)` tends to `f z`, for `z` in a disc centered at `0`,
when `f` is continuous on the closed disc. -/
lemma tendsto_of_radius_tendsto_one_of_continuousOn_closedDisc
    {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E] [CompleteSpace E]
    {f : ‚ÑÇ ‚Üí E} {z : ‚ÑÇ} {r : ‚Ñï ‚Üí ‚Ñù} {R : ‚Ñù}
    (hc : ContinuousOn f (closedBall 0 R)) (hr_lim : Tendsto r atTop (ùìù 1))
    (hz : z ‚àà ball 0 R) : Tendsto (fun n => f (r n * z)) atTop (ùìù (f z)) := by
  have h_seq : Tendsto (fun n => r n * z) atTop (ùìù z) := by
    simpa using Tendsto.mul (continuous_ofReal.continuousAt.tendsto.comp hr_lim)
      (tendsto_const_nhds (x := z))
  specialize hc z (ball_subset_closedBall hz)
  have hc : ContinuousAt f z := ContinuousWithinAt.continuousAt hc (closedBall_mem_nhds_of_mem hz)
  exact (ContinuousAt.tendsto hc).comp h_seq

/-- The sequence `r‚Çô = 1 - 1 / (n + 2)` is in `(0,1)` and tends to `1` as `n ‚Üí ‚àû`.
This sequence will be useful in applying the above convergence results. -/
lemma seq_tendsto_to_one_in_unit_interval_aux :
    let r : ‚Ñï ‚Üí ‚Ñù := fun n => 1 - 1 / (n + 2)
    (‚àÄ n, r n ‚àà Ioo 0 1) ‚àß Tendsto r atTop (ùìù 1) := by
  let r : ‚Ñï ‚Üí ‚Ñù := fun n => 1 - 1 / (n + 2)
  have hr (n : ‚Ñï) : r n ‚àà Ioo 0 1 := by
    simp only [one_div, mem_Ioo, sub_pos, sub_lt_self_iff, inv_pos, r]
    have : (1 : ‚Ñù) < n + 2 := by linarith
    exact ‚ü®inv_lt_one_of_one_lt‚ÇÄ this, by linarith‚ü©
  have hr_lim : Tendsto r atTop (ùìù 1) :=
    le_trans (tendsto_const_nhds.sub <| tendsto_const_nhds.div_atTop
      <| tendsto_atTop_add_const_right _ _ tendsto_natCast_atTop_atTop) (by rw [sub_zero])
  exact ‚ü®hr, hr_lim‚ü©

/-!
## Poisson Integrals on discs

We prove the Poisson integral formulae for harmonic functions, respectively for `‚ÑÇ`-differentiable
functions with values in a Banach space, defined on a disc centered at 0,
which extend continuously to the closed disc. The results are stated with `circleAverage`.
-/

/-- **Poisson integral formula for harmonic functions on a disc**:
A function `u` harmonic on a disc with radius `R` and center `0`,
and continuous on the closed disc, satisfies
`u(z) = (1/2œÄ) ‚à´_0^{2œÄ} (R¬≤ - |z|¬≤) / |R*e^{it} - z|¬≤ u(R*e^{it}) dt` for `z` in the disc. -/
theorem poisson_integral_of_harmonicOn_disc_continuousOn_closedDisc
    {u : ‚ÑÇ ‚Üí ‚Ñù} {z : ‚ÑÇ} {R : ‚Ñù}
    (hu : HarmonicOnNhd u (ball 0 R)) (hc : ContinuousOn u (closedBall 0 R)) (hz : z ‚àà ball 0 R) :
    u z = circleAverage (fun Œ∂ => ((R ^ 2 - ‚Äñz‚Äñ ^ 2) / ‚ÄñŒ∂ - z‚Äñ ^ 2) * u Œ∂) 0 R := by
  let r : ‚Ñï ‚Üí ‚Ñù := fun n => 1 - 1 / (n + 2)
  -- We approximate `1` by a sequence `r‚Çô` in `(0,1)`.
  obtain ‚ü®hr, hr_lim‚ü© := seq_tendsto_to_one_in_unit_interval_aux
  have h_poisson (n : ‚Ñï) := poisson_integral_of_harmonicOn_scaled_disc hu (hr n) hz
  simp only [circleAverage, circleMap, zero_add, ‚Üê mul_assoc] at ‚ä¢ h_poisson
  have hu_lim := tendsto_integral_prod_of_continuousOn_circle_closedDisc (pos_of_mem_ball hz) hc
                 (poisson_ker_continousOn_circle hz) hr hr_lim
  have hu_lim : Tendsto (fun n => (u (r n * z))) atTop (ùìù ((2 * œÄ)‚Åª¬π * ‚à´ t in 0..2 * œÄ,
      ((R ^ 2 - ‚Äñz‚Äñ^2) / ‚ÄñR * exp (t * I) - z‚Äñ^2 * u (R * exp (t * I))))) := by
    simp only [r, h_poisson]
    dsimp only [smul_eq_mul] at hu_lim
    exact (Tendsto.const_mul (2 * œÄ)‚Åª¬π hu_lim)
  -- We conclude by uniqueness of limits.
  rw [‚Üê tendsto_nhds_unique hu_lim
        (tendsto_of_radius_tendsto_one_of_continuousOn_closedDisc hc hr_lim hz)]
  rfl

/-- **Poisson integral formula for ‚ÑÇ-differentiable functions on a disc**:
A function `f : ‚ÑÇ ‚Üí E` `‚ÑÇ`-differentiable on a disc with radius `R` and center `0`,
and continuous on the closed disc, satisfies
`f(z) = (1/2œÄ) ‚à´_0^{2œÄ} (R¬≤ - |z|¬≤) / |R*e^{it} - z|¬≤ f(R*e^{it}) dt` for `z` in the disc. -/
theorem poisson_integral_of_diffContOnCl_disc
    {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚ÑÇ E] [CompleteSpace E] {f : ‚ÑÇ ‚Üí E} {z : ‚ÑÇ}
    {R : ‚Ñù} (hf : DiffContOnCl ‚ÑÇ f (ball 0 R)) (hz : z ‚àà ball 0 R) :
    f z = circleAverage (fun Œ∂ => ((R ^ 2 - ‚Äñz‚Äñ ^ 2) / ‚ÄñŒ∂ - z‚Äñ ^ 2) ‚Ä¢ f Œ∂) 0 R := by
  let r : ‚Ñï ‚Üí ‚Ñù := fun n => 1 - 1 / (n + 2)
  obtain ‚ü®hr, hr_lim‚ü© := seq_tendsto_to_one_in_unit_interval_aux
  have h_poisson (n : ‚Ñï) :=
      poisson_integral_of_differentiableOn_scaled_disc hf.differentiableOn (hr n) hz
  simp only [circleAverage, circleMap, zero_add, ‚Üê mul_assoc] at ‚ä¢ h_poisson
  have hc := DiffContOnCl.continuousOn_ball hf
  have hu_lim : Tendsto (fun n => (f (r n * z))) atTop (ùìù ((2 * œÄ)‚Åª¬π ‚Ä¢ ‚à´ t in 0..2 * œÄ,
      ((R ^ 2 - ‚Äñz‚Äñ ^ 2) / ‚ÄñR * exp (t * I) - z‚Äñ ^ 2) ‚Ä¢ f (R * exp (t * I)))) := by
    simp only [r, h_poisson]
    exact Tendsto.const_smul (tendsto_integral_prod_of_continuousOn_circle_closedDisc
      (pos_of_mem_ball hz) hc (poisson_ker_continousOn_circle hz) hr hr_lim) (2 * œÄ)‚Åª¬π
  rw [‚Üê tendsto_nhds_unique (hu_lim)
        (tendsto_of_radius_tendsto_one_of_continuousOn_closedDisc hc hr_lim hz)]

/-- **Poisson integral formula for harmonic functions on a disc**:
A function `u : ‚ÑÇ ‚Üí ‚Ñù` harmonic on a disc with radius `R` and center `0`, and
continuous on the closed disc, satisfies
`u(z) = (1/2œÄ) ‚à´_0^{2œÄ} Re((R*e^{it} + z) / (R*e^{it} - z)) * u(R*e^{it}) dt`
for `z` in the disc. -/
theorem poisson_integral_of_harmonicOn_disc_continuousOn_closedDisc_ker_re
    {u : ‚ÑÇ ‚Üí ‚Ñù} {z : ‚ÑÇ} {R : ‚Ñù}
    (hu : HarmonicOnNhd u (ball 0 R)) (hc : ContinuousOn u (closedBall 0 R)) (hz : z ‚àà ball 0 R) :
    u z = circleAverage (fun Œ∂ => ((Œ∂ + z) / (Œ∂ - z)).re * u Œ∂) 0 R := by
  rw [poisson_integral_of_harmonicOn_disc_continuousOn_closedDisc hu hc hz]
  refine circleAverage_congr_sphere ?_
  intro Œ∂ hŒ∂
  dsimp
  congr 1
  rw [mem_sphere, dist_zero_right, abs_of_pos (pos_of_mem_ball hz)] at hŒ∂
  exact (realPart_herglotz_ker_eq_poisson_ker Œ∂ z hŒ∂).symm

/-- **Poisson integral formula for ‚ÑÇ-differentiable functions on a disc**:
A function `f : ‚ÑÇ ‚Üí E` `‚ÑÇ`-differentiable on a disc with radius `R` and center `0`,
and continuous on the closed disc, satisfies
`f(z) = (1/2œÄ) ‚à´_0^{2œÄ} Re((R*e^{it} + z) / (R*e^{it} - z)) ‚Ä¢ f(R*e^{it}) dt`
for `z` in the disc. -/
theorem poisson_integral_of_diffContOnCl_disc_ker_re
    {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚ÑÇ E] [CompleteSpace E] {f : ‚ÑÇ ‚Üí E} {z : ‚ÑÇ}
    {R : ‚Ñù} (hf : DiffContOnCl ‚ÑÇ f (ball 0 R)) (hz : z ‚àà ball 0 R) :
    f z = circleAverage (fun Œ∂ => ((Œ∂ + z) / (Œ∂ - z)).re ‚Ä¢ f Œ∂) 0 R := by
  rw [poisson_integral_of_diffContOnCl_disc hf hz]
  refine circleAverage_congr_sphere ?_
  intro Œ∂ hŒ∂
  dsimp
  congr 1
  rw [mem_sphere, dist_zero_right, abs_of_pos (pos_of_mem_ball hz)] at hŒ∂
  exact (realPart_herglotz_ker_eq_poisson_ker Œ∂ z hŒ∂).symm
